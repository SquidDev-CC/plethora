// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.2.0'
	id "com.github.ben-manes.versions" version "0.21.0" // Provides dependencyUpdates task.
}

buildDir = "build"

apply plugin: 'net.minecraftforge.gradle.forge'

version = mc_version + "-" + mod_version
group = "org.squiddev"
archivesBaseName = "plethora"

minecraft {
	version = "${project.mc_version}-${project.forge_version}"
	runDir = "run"
	mappings = "snapshot_20180814"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
	replace '${cct_version}', project.cct_version
	replace '${forge_version}', project.forge_version
}

jar {
	manifest {
		attributes(
			'FMLAT': 'Plethora_at.cfg',
		)
	}

	exclude "**/*.java"
}

configurations {
	integrationMod
	compileOnly.extendsFrom integrationMod
	testCompile.extendsFrom integrationMod
	testCompile.extendsFrom forgeGradleGradleStart
}

repositories {
	jcenter()
	mavenLocal()

	maven { url 'http://chickenbones.net/maven/' } // CBMP
	maven { url 'http://dvs1.progwml6.com/files/maven/' } // JEI + Tinker's
	maven { url 'http://maven.ic2.player.to/' } // IC2 + Forestry
	maven { url 'https://dl.bintray.com/squiddev/maven/' } // ConfigGen (why do I use this still?)
	maven { url 'https://maven.amadornes.com/' } // Multipart
	maven { url 'https://maven.blamejared.com/' } // Botania
	maven { url 'https://maven.covers1624.net/' } // CoFH
	maven { url 'https://maven.k-4u.nl/' } // McJty's mods
	maven { url 'https://maven.mcmoddev.com/' } // Tesla
	maven { url 'https://maven.tterrag.com/' } // EnderCore
	maven { url 'https://maven2.tterrag.com/' } // EnderIO
	maven { url 'https://mod-buildcraft.com/maven/' } // Buildcraft
	maven { url 'https://maven.hellfiredev.net/' } // Astral Sorcery
	maven { url 'https://repo.raoulvdberge.com/' } // Refined Storage
	maven { url 'https://squiddev.cc/maven/' } // CC: Tweaked
	maven { url 'https://minecraft.curseforge.com/api/maven/' } // Everything else
}

dependencies {
	deobfCompile "org.squiddev:cc-tweaked-${mc_version}:${cct_version}"

	compile "mezz.jei:jei_1.12.2:4.15.0.268"

	// External mods with maven dependencies. Have fun when the next version of Minecraft drops!
	integrationMod "appeng:appliedenergistics2:rv6-stable-6"
	integrationMod "codechicken:ChickenASM:1.12-1.0.2.9"
	integrationMod "codechicken:CodeChickenLib:1.12.2-3.2.2.354:universal"
	integrationMod("codechicken:ForgeMultipart:1.12.2-2.6.1.81:universal") { exclude group: "codechicken" }
	integrationMod "cofh:CoFHCore:1.12.2-4.6.2.25:universal"
	integrationMod "cofh:RedstoneFlux:1.12-2.1.0.7:universal"
	integrationMod "cofh:ThermalExpansion:1.12.2-5.5.3.41:universal"
	integrationMod "cofh:ThermalFoundation:1.12.2-2.6.2.26:universal"
	integrationMod "cofh:CoFHWorld:1.12.2-1.3.0.6:universal"
	compileOnly   ("com.enderio:EnderIO:1.12.2-5.0.43") { transitive = false }
	compileOnly    "com.enderio.core:EnderCore:1.12.2-0.5.57" // EnderCore appears to crash in a development environment.
	integrationMod("com.github.mcjty:mcjtylib:1.12-3.5.0") { transitive = false }
	integrationMod("com.github.mcjty:rftools:1.12-7.70") { transitive = false }
	integrationMod("com.github.mcjty:xnet:1.12-1.8.0") { transitive = false }
	integrationMod "com.mod-buildcraft:buildcraft-all:7.99.22"
	integrationMod "hellfirepvp.astralsorcery:astralsorcery:1.12.2-1.10.18-v129"
	integrationMod "MCMultiPart2:MCMultiPart:2.5.4"
	integrationMod "net.darkhax.tesla:Tesla-1.12.2:1.0.63"
	integrationMod "net.industrial-craft:industrialcraft-2:2.8.146-ex112"
	integrationMod "net.sengir.forestry:forestry_1.12.2:5.8.2.387:api"
	integrationMod "net.sengir.forestry:forestry_1.12.2:5.8.2.387"
	integrationMod "refinedstorage:refinedstorage:1.6.13-364"
	integrationMod "slimeknights:TConstruct:1.12.2-2.12.0.116"
	integrationMod "slimeknights.mantle:Mantle:1.12-1.3.3.51"
	integrationMod "vazkii.botania:Botania:r1.10-362.77"

	// All the Curse mods
	integrationMod "baubles:Baubles:1.12:1.5.2"
	integrationMod "storage-drawers:StorageDrawers:1.12.2:5.3.8"
	integrationMod "chameleon:Chameleon:1.12:4.1.3"
	integrationMod "blockcraftery:blockcraftery:1.12.2:1.2.0"
	integrationMod "mysticallib:mysticallib:1.12.2:1.1.1"
	integrationMod "roost:roost:1.12:1.3.0"
	integrationMod "chickens:chickens:6.0.4"
	integrationMod "hatchery:hatchery:1.12.2:2.2.1"

	compileOnly('org.squiddev:ConfigGen:1.2.5') { exclude group: 'net.minecraftforge' }

	// compileOnly "org.squiddev:forgelint:0.1.1"
	compileOnly "com.google.errorprone:error_prone_annotations:2.0.21"

	testCompile 'junit:junit:4.11'
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes" << "-Xlint:-processing" << "-parameters"
	}
}

// Not quite ready for common usage, but helped a lot in checking stuff
// compileJava { options.compilerArgs << "-Xplugin:ForgeLint" }

// Added for mods that have per-player data, e.g. Astral Sorcery
runClient {
	args '--username', 'PlethoraDev'
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'release'
		changelog = "Release notes can be found on the GitHub repository (https://github.com/SquidDev-CC/plethora/releases/tag/v${project.version})."

		 relations {
			 requiredLibrary 'cctweaked'
		 }
	}
}

task runTestServer(type: JavaExec) {
  classpath = sourceSets.test.runtimeClasspath
  main = "org.squiddev.plethora.boostrap.LaunchServer"
  workingDir = "${project.projectDir}/test-files/server"

  workingDir.mkdirs()
}
