// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.5'
}

buildDir = "build-${mc_version}"

apply plugin: 'net.minecraftforge.gradle.forge'

version = mc_version + "-" + mod_version
group = "org.squiddev"
archivesBaseName = "plethora"

minecraft {
	version = "${project.mc_version}-${project.forge_version}"
	runDir = "run-${project.mc_version}"
	mappings = "snapshot_20161111"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
	replace '${cc_version}', project.cc_version
}

jar {
	manifest {
		attributes(
			'FMLAT': 'Plethora_at.cfg',
		)
	}

	exclude "**/*.java"
}

repositories {
	jcenter()

	// JEI + Tinker's
	maven { url = "http://dvs1.progwml6.com/files/maven" }

	// ComputerCraft
	maven { url "https://cc.crzd.me/maven/" }

	// Chameleon + Storage Drawers
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2349/653/[module]-[revision](.[ext])" }
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2353/147/[module]-[revision](.[ext])" }

	// CCTweaks + ConfigGen
	maven { url = "https://dl.bintray.com/squiddev/maven" }

	// IC2 API
	maven { url = "http://maven.ic2.player.to/" }

	// Buildcraft
	ivy {
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]"
	}

	// Multipart
	maven { url "http://maven.amadornes.com/" }

	// Refined Storage
	maven { url "http://dl.bintray.com/raoulvdberge/dev" }

	// Tesla
	maven { url 'http://maven.epoxide.xyz' }

	// Forestry
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2358/227/[module]_[revision](.[ext])" }

	// Applied Energistics 2
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2356/184/[module]-[revision](.[ext])" }

	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2424/565/[module]-[revision](.[ext])" }

	// CBMP
	maven { url "http://chickenbones.net/maven" }
}

dependencies {
	compile "dan200.computercraft:ComputerCraft:${project.cc_version}-build10"
	deobfCompile "mezz.jei:jei_${project.mc_version}:3.13.6.392"

	compileOnly "org.squiddev:CCTweaks:${project.mc_version}-1.5.0-pr0:dev"
	compileOnly "net.industrial-craft:industrialcraft-2:2.6.99-ex110:dev"
	compileOnly "buildcraft:buildcraft:7.2.7:dev" // Won't run on 1.10.2 but just here for the future
	compileOnly "net.darkhax.tesla:Tesla:${project.mc_version}-1.2.1.50:deobf"

	// Tinker's + Dependencies
	compileOnly "slimeknights:TConstruct:${project.mc_version}-2.6.1.464"
	compileOnly "slimeknights.mantle:Mantle:${project.mc_version}-1.1.3.199"

	// Storage drawers + Dependencies
	compileOnly "curseMods:Chameleon:1.10-2.2.0"
	compileOnly "curseMods:StorageDrawers:${project.mc_version}-3.5.8"

	compileOnly "MCMultiPart:MCMultiPart:1.4.0:universal"
	compileOnly "refinedstorage:refinedstorage:1.2.5-576"
	compileOnly "curseMods:forestry:${project.mc_version}-5.2.16.340"
	compileOnly "curseMods:appliedenergistics2:rv4-alpha-9"
	compileOnly "curseMods:Baubles:${project.mc_version}-1.3.11"
	compileOnly "codechicken:ForgeMultipart:1.10.2-2.0.0.31:deobf"

	compileOnly('org.squiddev:ConfigGen:1.2.5') { exclude group: 'net.minecraftforge' }
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes" << "-Xlint:-processing"
	}
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'release'
		changelog = ''

		relations {
			requiredLibrary 'computercraft'
		}
	}
}
